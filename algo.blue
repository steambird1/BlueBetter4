run "Algo.Blue -- BlueBetter Algorithms' Library."
run "By steambird1 2022."

class disjoint:
	function find obj:
		if this:obj=null:
			set this:obj=obj
			return obj
		elif this:obj=obj:
			return obj
		else:
			set result=this.find (this:obj)
			set this:obj=result
			return result
	function union a b:
		set this:(this.find a)=(this.find b)
	function is_same a b:
		return (this.find a)=(this.find b)

run "Using quick sort"
class sorter:
	function setlist listobj:
		set this.lister=listobj
	function _sort left right:
		if not (left+1<right):
			return null
		set lister=object this.lister
		set mid=floor ((left+right)/2)
		set i=left
		set j=right
		set align=lister:mid
		while i<j:
			while (i<lister.length)&(lister:i<align):
				set i=i+1
			while (not (j<0))&(lister:j>align):
				set j=j-1
			if not (i<j):
				break
			set tmp=lister:i
			set lister:i=lister:j
			set lister:j=tmp
			set i=i+1
			set j=j-1
		set this.lister=serial lister
		run this._sort left,j
		run this._sort i,right
	function sort:
		set lister=object this.lister
		run this._sort 0,(lister.length-1)
